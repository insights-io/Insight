plugins {
    id "java"
    id "jacoco"
    id "io.freefair.lombok"
    id "checkstyle"
    id "pmd"
    id "com.github.spotbugs"
    id "com.diffplug.spotless"
}

// We are ignoring this folder in Dockerfile
if (new File("$rootDir/gradle/ci-support.gradle").exists()) {
    apply from: "$rootDir/gradle/ci-support.gradle"
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    group "org.rebrowse"
    version "1.0"
}


subprojects {
    apply plugin: "java"
    apply plugin: "jacoco"
    apply plugin: "io.freefair.lombok"
    apply plugin: "checkstyle"
    apply plugin: "pmd"
    apply plugin: "com.github.spotbugs"
    apply plugin: "com.diffplug.spotless"

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(15)
        }
    }

    compileJava {
        options.encoding = "UTF-8"
        options.compilerArgs << "-parameters"
    }

    compileTestJava {
        options.encoding = "UTF-8"
    }

    dependencies {
        implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
    }

    jacoco {
        toolVersion = "0.8.6"
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled false
            csv.enabled = false
        }
    }

    test {
        finalizedBy(tasks.jacocoTestReport)
        systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
        testLogging {
            showStandardStreams = true
            showCauses = true
            showExceptions = true
            showStackTraces = true
            exceptionFormat "full"
        }
    }

    checkstyle {
        toolVersion = "8.36.2"
        configFile = rootProject.file("config/checkstyle/.checkstyle.xml")
        configProperties = [
                "checkstyle.cache.file": "${buildDir}/checkstyle.cache",
        ]
    }

    pmd {
        consoleOutput = true
        ignoreFailures = true
        toolVersion = "6.28.0"
        ruleSets = []
        ruleSetFiles = rootProject.files("config/pmd/ruleset.xml")
    }

    spotbugs {
        ignoreFailures = true
        toolVersion = "4.1.3"
    }

    spotless {
        java {
            googleJavaFormat("1.9")
            removeUnusedImports()
        }
    }
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled true
        html.enabled false
    }
}
