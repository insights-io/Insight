plugins {
    id 'java'
    id 'jacoco'
    id 'io.freefair.lombok'
    id 'checkstyle'
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    group 'org.meemaw'
    version '1.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'io.freefair.lombok'
    apply plugin: "checkstyle"

    java {
        sourceCompatibility = JavaVersion.VERSION_14
        targetCompatibility = JavaVersion.VERSION_14
    }

    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-parameters'
    }

    compileTestJava {
        options.encoding = 'UTF-8'
    }

    dependencies {
        implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled false
            csv.enabled = false
        }
    }

    test {
        finalizedBy(tasks.jacocoTestReport)
        systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
        testLogging.showStandardStreams = true
    }

    checkstyle {
        toolVersion = '8.32'
        configFile = rootProject.file('.checkstyle.xml')
        configProperties = [
                'checkstyle.cache.file': "${buildDir}/checkstyle.cache",
        ]
    }
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled true
        html.enabled false
    }
}
